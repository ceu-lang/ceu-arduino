/* TODO: only working with original Arduino (but see other TODO comment below) */

native/pre do
    ##define CEU_FEATURES_ISR_SLEEP_MIN
end

#include "arduino/arduino.ceu"
#include "arduino/avr.ceu"
#include "wclock.ceu"

native/pure _BAUD_USART;
native/pre do
#ifdef __AVR_ATmega644P__
    ##define BAUD_USART(baud_human) (((F_CPU / (baud_human * 16UL))) - 1)
#else
    ##define BAUD_USART(baud_human) ((F_CPU / 4 / baud_human - 1) / 2)
#endif
end

input byte USART_RX;
input none USART_TX;

#ifdef ARDUINO_BOARD_MEGA
spawn async/isr [_USART0_RX_vect_num,0] do
#else
spawn async/isr [_USART_RX_vect_num,0] do
#endif
    var byte c = _UDR0;
    emit USART_RX(c);
end

#ifdef ARDUINO_BOARD_MEGA
spawn async/isr [_USART0_TX_vect_num,0] do
#else
spawn async/isr [_USART_TX_vect_num,0] do
#endif
    emit USART_TX;
end

{
    UCSR0A = 1 << U2X0; // TODO: maybe now works on other Arduino's??

    UBRR0H = (BAUD_USART(9600)>>8);     // set baud rate
    UBRR0L = (BAUD_USART(9600));
    UCSR0C = (1<<USBS0)|(3<<UCSZ00);    // 8data, 2stop-bit
    UCSR0B = (1<<RXEN0)  | (1<<TXEN0)   // enable RX/TX
           | (1<<RXCIE0) | (1<<TXCIE0); // enable interrupts
}
do finalize with
    _UCSR0B = 0;                        // disable TX/RX & interrups
end

loop do
    var byte c = await USART_RX;
    _ceu_assert(_UCSR0A & (1<<_UDRE0), "bug found");
    _UDR0 = c;
end
