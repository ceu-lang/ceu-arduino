input int DT;
input int PIN02;
input int PIN03;

native @nohold _queue_put();
native @pure   _queue_get();

native do
    enum {
        TM_REQ_PAUSE = -1,
    };
    ##define assert(x)
end

class TimeMachine with
    var App& app;
    event void go_on, go_off;
    //event void ok_restart;

    event int  go_seek;
    event void go_pause;
    event int  go_backward;
    event int  go_forward;

    var   bool seeking? = false;
    event void ok;
    #define TM_AWAIT_SEEK(me)  \
        if me.seeking? then    \
            await me.ok;       \
        end

    var int time_total = 0;
    var int time_now;
do
    _SNAP_data := (char*) &this.app;
    _QUEUE_get = 0;
    _SNAP_put = 0;

    _snap_put(_QUEUE_put, this.time_total);     // 0, 0
    var int next_snap = _SNAP_ms;

    loop do
        watching this.go_on do
            par do
                var int dt;
                every dt in DT do
                    this.time_total = this.time_total + dt;
                    _queue_put(_CEU_IN_DT, _EVTP(dt), 0, null);
                    if this.time_total >= next_snap then
                        next_snap = next_snap + _SNAP_ms;
                        _snap_put(_QUEUE_put, this.time_total);
                    end
                end
            with
                var int v;
                every v in PIN02 do
                    _queue_put(_CEU_IN_PIN02, _EVTP((void*)null), sizeof(int), (byte*)v);
                end
            with
                var int v;
                every v in PIN03 do
                    _queue_put(_CEU_IN_PIN03, _EVTP((void*)null), sizeof(int), (byte*)v);
                end
            end
        end

        _CEU_TIMEMACHINE_ON = _CEU_TIMEMACHINE_ON + 1;

        par/or do
            finalize with
                _CEU_TIMEMACHINE_ON = _CEU_TIMEMACHINE_ON - 1;
                this.time_total = time_now;     // drop future
                _QUEUE_put = _QUEUE_get;
                _QUEUE_tot = _QUEUE_get;
                next_snap = _snap_set(time_now);
            end
            await this.go_off;
        with
            var int speed = 0;
                // initialization avoids valgrind "uninitalized access" below
            event int e_backward;
            var int time_req = _TM_REQ_PAUSE;
            time_now = this.time_total; // start in the end (the current state)
            _QUEUE_get = _QUEUE_put;

            par do
                loop do
                    par/or do
                        var int tmp = await this.go_seek;
                                    // avoid conflict w/ ongoing time_req
                        TM_AWAIT_SEEK(this);
                        time_req = tmp;
                        this.time_now = _snap_get(time_req);
                        speed = 0;
//_printf("=== SEEK: [%d] => [%d]\n", time_now, time_req);
                        //emit this.ok_restart;
                        //_queue_reset();
                    with
                        await this.go_pause;
                        TM_AWAIT_SEEK(this);
//_printf("=== PAUSE ===\n");
                        time_req = _TM_REQ_PAUSE;
                    with
                        var int s = await this.go_backward;
                        TM_AWAIT_SEEK(this);
                        speed = s;
//_printf("=== BACKWARDS: [%d] => ??\n", time_now);
                        emit e_backward => s;
                        await FOREVER;          // e_backward emits go_seek
                    with
                        var int s = await this.go_forward;
                        TM_AWAIT_SEEK(this);
                        speed    = s;
                        time_req = this.time_total;
//_printf("=== FORWARD: [%d] => [%d]\n", time_now, time_req);
                    with
                        if speed == 0 then
                            this.seeking? = true;
                        end
                        async (&time_now, speed, time_req, time_total) do
//_printf("=== seek (%d => %d || %d) ===\n", time_now, time_req, time_total);
                            loop do
                                if time_req == _TM_REQ_PAUSE then
//_printf("=== break: pause ===\n");
                                    break;  // already there
                                end
                                var int get = _QUEUE_get;
                                var _tceu_queue[] qu = _queue_get();
                                if qu == null then
//_printf("=== break: e=null ===\n");
                                    break;
                                end
//_printf("|\t[get=%d]  evt=%d v=%d\n", get, (int)qu:evt, (int)qu:param.v);
                                //_assert(qu != null);
                                if qu:evt == _CEU_IN_DT then
                                    var int dt = qu:param.v;
                                    if time_now+dt > time_req then
                                        _QUEUE_get = get;   // undo get
//_printf("=== break: >time_req ===\n");
                                        break;  // stop before this emission
                                    end
//_printf("[%d] => dt=%d , %d-%d\n", time_now, dt, get, _QUEUE_get);
                                    time_now = time_now + dt;
                                    emit (dt)ms;
                                    emit DT => dt;
                                    if speed > 0 then
                                        _delay(dt/speed);
                                    else/if speed < 0 then
                                        _delay(dt*-speed);
                                    end
                                else
                                    _assert(0);
                                end
                                if speed != 0 then
                                    //emit SDL_REDRAW;
                                end
                            end
                            //emit SDL_REDRAW;
                        end
//_printf("=== EO-Seek ===\n");
                        if this.seeking? then
                            this.seeking? = false;
                            emit this.ok;
                        end
                        await FOREVER;
                    end
                end
            with
                var bool go? = false;
                var int bk_speed;
                loop do
                    var bool me? = false;
                    par/or do
                        // start on this
                        bk_speed = await e_backward;
                        go? = true;
                    with
                        // stop on any of these
                        par/or do
                            await go_seek until (not me?);
                        with
                            await go_pause;
                        with
                            await go_forward;
                        end
                        go? = false;
                    with
                        if not go? then
                            await FOREVER;
                        end
                        var int now = this.time_now;
//_printf("=======back from now [%d]\n", now);
                        loop i do
                            now = now - 30;
                            if now < 0 then
                                now = 0; // one last time to stop exactly at 0
                            end
                            me? = true;
                            emit this.go_seek => now;   // don't awake myself
                            me? = false;
                            par/and do
                                await this.ok;
                            with
                                var int awt;
                                if bk_speed > 0 then
                                    awt = 30/bk_speed;
                                else/if bk_speed < 0 then
                                    awt = 30*-bk_speed;
                                else
                                    _assert(0);
                                end
                                await (awt)ms_;
                            end
                            if now == 0 then
                                break;
                            end
                        end
                        await FOREVER;
                    end
                end
            end
        end
    end

    native do
        ##ifdef __cplusplus
        ##define EVTP(v) (tceu_evtp(v))
        ##else
        ##define EVTP(v) ((tceu_evtp)v)
        ##endif

        typedef struct {
            tceu_nevt evt;
            tceu_evtp param;
            s32       sz;
            byte      buf[0];
        } tceu_queue;

        #define QUEUE_MAX 60*60*1000           \
                          * sizeof(tceu_queue) \
                          / 30 * 2
                            // 60min*60sec*1000ms=(1h in ms)
                            // * minimum size of an event
                            // 30FPS * 2events/frame
            // 1h of events with 2 events/frame
            // 3600*1000 = 3600000 =>/30 = 120000 =>2 = 240000 = 240K events

        #undef QUEUE_MAX
        #define QUEUE_MAX 1000

        byte QUEUE[QUEUE_MAX];
        int  QUEUE_tot = 0;
        u32  QUEUE_get = 0;
        u32  QUEUE_put = 0;

        int queue_put (tceu_nevt evt, tceu_evtp param,
                               int sz, byte* buf) {
            int n = sizeof(tceu_queue) + sz;

            if (QUEUE_tot+n > QUEUE_MAX)
                assert(0);
                //return 0;   /* TODO: add event FULL when QUEUE_MAX-1 */

            /* An event+data must be continuous in the QUEUE. */
            if (QUEUE_put+n+sizeof(tceu_queue)>=QUEUE_MAX && evt!=CEU_IN__NONE) {
                assert(0);
                int fill = QUEUE_MAX - QUEUE_put - sizeof(tceu_queue);
                tceu_queue* qu = (tceu_queue*) &QUEUE[QUEUE_put];
                qu->evt = CEU_IN__NONE;
                qu->sz  = fill;
                QUEUE_put += sizeof(tceu_queue) + fill;
                QUEUE_tot += sizeof(tceu_queue) + fill;
            }

            {
                tceu_queue* qu = (tceu_queue*) &QUEUE[QUEUE_put];
                qu->evt = evt;
                qu->sz  = sz;

                if (sz == 0) {
                    /* "param" is self-contained */
                    qu->param = param;
                } else {
                    /* "param" points to "buf" */
                    qu->param.ptr = qu->buf;
                    memcpy(qu->buf, buf, sz);
                }
            }
            QUEUE_put += n;
            QUEUE_tot += n;

            return 1;
        }

        tceu_queue* queue_get (void) {
            if (QUEUE_get == QUEUE_put) {
                return NULL;
            } else {
                assert(QUEUE_tot > 0);
                tceu_queue* qu = (tceu_queue*) &QUEUE[QUEUE_get];
                QUEUE_get += sizeof(tceu_queue) + qu->sz;
                return qu;
            }
        }

    ///////////////////////////////////////////////////////////////////////////////

        #define SNAP_ms 2000       // 10s = 30*10 = at most 300 frames to traverse
        #define SNAP_N   6*60       // 10s*6*60=1h  (360*56K-rocks = 1.8Mb)

        typedef struct {
            CEU_App data;
            u32     time;
            u32     queue;
        } tceu_snap;

        tceu_snap SNAP[SNAP_N];
        int SNAP_put = 0;
        char* SNAP_data;

        void snap_put (u32 queue, u32 time) {
            assert(SNAP_put < SNAP_N);
            SNAP[SNAP_put].data  = *((CEU_App*)SNAP_data);
            SNAP[SNAP_put].queue = queue;
            SNAP[SNAP_put].time  = time;
            SNAP_put++;
        }

        u32 snap_set (u32 time) {
            int i;
            for (i=0; i<SNAP_put; i++) {
                if (SNAP[i].time > time) {
                    SNAP_put = i;
                    return SNAP[i].time;
                }
            }
            return SNAP[SNAP_put-1].time + SNAP_ms;
        }

        u32 snap_get (u32 time) {
            int i;  // SNAP[0] is guaranteed to succeed
            for (i=SNAP_put-1; i>=0; i--) {
                if (SNAP[i].time <= time) {
                    *((CEU_App*)SNAP_data) = SNAP[i].data;
                    QUEUE_get = SNAP[i].queue;
                    return SNAP[i].time;
                }
            }
            assert(0);
        }
/*
        void dump () {
            int i;
            for (i=0; i<SNAP_put; i++) {
                tceu_snap snap = SNAP[i];
                snap_get(snap.time);
                printf("SNAP[%d]  time=%d  queue=%d\n", i, snap.time, snap.queue);
                tceu_queue* qu;
                u32 get = QUEUE_get;
                int time = snap.time;
                while (qu = queue_get()) {
                    printf("|\t[get=%d] [now=%d]  %d=>%d\n",
                            get, time, qu->evt, qu->param);
                    if (qu->evt == CEU_IN_SDL_DT)
                        time += qu->param.v;
                    get = QUEUE_get;
                }
            }
        }
*/
    end
end
